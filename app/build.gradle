apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion compile_sdk
    buildToolsVersion build_tools

    defaultConfig {
        applicationId "info.dvkr.screenstream"
        minSdkVersion min_sdk
        targetSdkVersion target_sdk
        versionCode version_code
        versionName version_name
        resConfigs "en", "ru", "pt-rBR", "zh-rTW", "fr-rFR", "fa", "it"

        vectorDrawables.useSupportLibrary = true

//        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        fabricfree {
            ext.enableCrashlytics = false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            ext.enableCrashlytics = false
            minifyEnabled false //true
            shrinkResources false //true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        release.java.srcDirs += 'src/release/kotlin'
        fabricfree.java.srcDirs += 'src/fabricfree/kotlin'

        main.java.srcDirs += [file("$buildDir/generated/source/kapt/main")]
        debug.java.srcDirs += [file("$buildDir/generated/source/kapt/debug")]
        release.java.srcDirs += [file("$buildDir/generated/source/kapt/release")]
        fabricfree.java.srcDirs += [file("$buildDir/generated/source/kapt/fabricfree")]

        main.java.srcDirs += [file("$buildDir/generated/source/kaptKotlin/main")]
        debug.java.srcDirs += [file("$buildDir/generated/source/kaptKotlin/debug")]
        release.java.srcDirs += [file("$buildDir/generated/source/kaptKotlin/release")]
        fabricfree.java.srcDirs += [file("$buildDir/generated/source/kaptKotlin/fabricfree")]
    }

    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'
    }

    configurations {
        debugCompile
        releaseCompile
        fabricfreeCompile
    }
}

androidExtensions { experimental = true }
kapt { useBuildCache = true }

dependencies {
    implementation project(':data')

    implementation "org.jetbrains:annotations:$annotations_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"

    implementation "com.google.android.material:material:1.0.0"
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-alpha2"

    implementation 'com.andrognito.flashbar:flashbar:1.0.3'
    implementation 'com.github.florent37:expansionpanel:1.2.2'
    implementation 'com.jjoe64:graphview:4.2.2'
    implementation 'com.afollestad.material-dialogs:core:2.0.0-rc3'
    implementation 'com.afollestad.material-dialogs:color:2.0.0-rc3'
    implementation 'com.afollestad.material-dialogs:input:2.0.0-rc3'

    implementation "org.koin:koin-android:$koin_version"
    implementation "com.github.iamironz:binaryprefs:$binaryprefs_version"
    implementation "com.jakewharton.timber:timber:$timber_version"

    releaseImplementation 'com.google.firebase:firebase-core:16.0.6'
    releaseImplementation 'com.crashlytics.sdk.android:crashlytics:2.9.7'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.2'
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.2'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.2'

//    testImplementation 'junit:junit:4.12'
//    androidTestImplementation 'com.android.support.test:runner:1.0.2'
//    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}